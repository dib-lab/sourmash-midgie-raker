import pprint

configfile: '../config.yaml'
print('config from ../config.yaml is:')
pprint.pprint(config)

RAW_GENOME_LOCATION=config.get('base')['bin_location'].rstrip('/')
EXTENSION=config.get('base')['extension']
NAMES, = glob_wildcards(RAW_GENOME_LOCATION + '/' + '{name}' + '.' + EXTENSION)

print('using bin location:', RAW_GENOME_LOCATION)
print('using bin extension:', EXTENSION)
print(f'found {len(NAMES)} raw genomes.')
print(f'example name:', NAMES[0] + '.' + EXTENSION)

###

import csv

rule all:
    input:
        'manysketch-raw.csv',
        'bin-sketches.sig.zip',

rule make_manysketch_csv:
    output:
        'manysketch-raw.csv'
    run:
        with open(output[0], 'w', newline='') as fp:
            w = csv.writer(fp)
            w.writerow(['name', 'genome_filename', 'protein_filename'])
            for name in NAMES:
                location = f"{RAW_GENOME_LOCATION}/{name}.{EXTENSION}"
                w.writerow([name, location, ''])

rule sketch:
    input:
        'manysketch-raw.csv',
    output:
        'bin-sketches.sig.zip',
    threads: 64
    params:
        sketchtype="dna,k=31"
    shell: """
        sourmash scripts manysketch -c {threads} {input} -o {output} \
            -p {params.sketchtype}
    """
