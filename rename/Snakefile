import pprint

configfile: '../config.yaml'
print('config from ../config.yaml is:')
pprint.pprint(config)

RAW_GENOME_LOCATION=config.get('base')['bin_location'].rstrip('/')
EXTENSION=config.get('base')['extension']
NAMES, = glob_wildcards(RAW_GENOME_LOCATION + '/' + '{name}' + '.' + EXTENSION)

print('using bin location:', RAW_GENOME_LOCATION)
print('using bin extension:', EXTENSION)
print(f'found {len(NAMES)} raw genomes.')
print(f'example name:', NAMES[0] + '.' + EXTENSION)

SKETCHES='../sketch-raw/bin-sketches.sig.zip'

GTDB_DB = '/group/ctbrowngrp5/sourmash-db.new/gtdb-rs226/gtdb-rs226-k31.dna.rocksdb'
GTDB_TAX = '/group/ctbrowngrp5/sourmash-db.new/gtdb-rs226/gtdb-rs226.lineages.sqldb'

RANKS = 'superkingdom,phylum,class,order,family,genus,species'.split(',')

KEEP_ORIGINAL_IDENT=config.get('rename')['keep_original_ident']
NEW_PREFIX=config.get('rename')['new_prefix']

###

import csv
import sourmash


FILE_INFO = {}
ORIG_NAME = {}
for name in NAMES:
    ident = name.split(' ')[0]
    FILE_INFO[ident] = RAW_GENOME_LOCATION + '/' + name + '.' + EXTENSION
    ORIG_NAME[ident] = name


rule all:
    input:
        'raw-sketch-gather.csv',
        'raw-sketch-gather.with-lineages.csv',
        'raw-sketch-gather.classifications.csv',
        'bin-sketches.lineages.csv',
        'raw-sketch.unclassified.csv',
        'manysketch-renamed.csv',
        'bin-sketches.renamed.sig.zip',
        'bin-sketches.taxburst.html',


rule gather_raw_sketches:
    input:
        SKETCHES
    output:
        'raw-sketch-gather.csv',
    threads: 1                  # against rocksdb, yah?
    shell: """
        sourmash scripts fastmultigather {input} {GTDB_DB} \
           -k 31 --threshold-bp=100_000 --scaled=10_000 \
           -o {output} -c {threads}
    """

rule tax_annotate:
    input:
        'raw-sketch-gather.csv',
    output:
        'raw-sketch-gather.with-lineages.csv',
    shell: """
        sourmash tax annotate -g {input} -t {GTDB_TAX}
    """

rule tax_genome:
    input:
        'raw-sketch-gather.csv',
    output:
        'raw-sketch-gather.classifications.csv'
    params:
        prefix='raw-sketch-gather',
    shell: """
        sourmash tax genome -g {input} -t {GTDB_TAX} -o {params.prefix}
    """

rule process_csv:
    input:
        classify='raw-sketch-gather.classifications.csv',
        sketches=SKETCHES,
    output:
        lineages='bin-sketches.lineages.csv',
        unclass='raw-sketch.unclassified.csv',
        rename='manysketch-renamed.csv',
    run:
        with open(input.classify, 'r', newline='') as fp:
            r = csv.DictReader(fp)

            rows = list(r)

        lin_w = csv.writer(open(output.lineages, 'w', newline=''))
        lin_w.writerow(['ident'] + RANKS)

        unclass_w = csv.writer(open(output.unclass, 'w', newline=''))
        unclass_w.writerow(['ident'])

        rename_w = csv.writer(open(output.rename, 'w', newline=''))
        rename_w.writerow(['name', 'genome_filename', 'protein_filename'])

        found = set()
        n_classify = 0
        n_unclass = 0

        for n, row in enumerate(rows):
            ident = row['query_name'].split(' ')[0]
            assert ident in FILE_INFO
            found.add(ident)

            # calculate new ident, if desired
            if KEEP_ORIGINAL_IDENT:
                new_ident = ident
            else:
                new_ident = f"{NEW_PREFIX}_MAG{n}"

            # can we write a species classification?
            classify_at_species = False
            if row['status'] == 'match':
                lin = row['lineage'].split(';')
                if len(lin) == len(RANKS):
                    classify_at_species = True
                    n_classify += 1

                    lin_w.writerow([new_ident] + lin)

                    new_name = f"{new_ident} {lin[-1]} ({ident})"

            # if not, that's just fiiiine.
            if not classify_at_species:
                new_name = f"{new_ident} unknown ({ident})"
                unclass_w.writerow([ident])
                n_unclass += 1

            # write out new name
            genomefile = FILE_INFO[ident]
            rename_w.writerow([new_name, genomefile, ''])

        # check/confirm
        db = sourmash.load_file_as_index(input.sketches)
        mf = db.manifest

        # one source of unclassified is no species match; another is no
        # gather match!
        # CTB: clean up/refactor so this is a single loop :(
        for row in mf.rows:
            ident = row['name'].split(' ')[0]
            assert ident, f"'{ident}' is empty for {row['name']}"
            if ident not in found:
                found.add(ident)

                # calculate new ident, if desired
                n += 1
                if KEEP_ORIGINAL_IDENT:
                    new_ident = ident
                else:
                    new_ident = f"{NEW_PREFIX}_MAG{n}"

                new_name = f"{new_ident} unknown ({ident})"
                genomefile = FILE_INFO[ident]
                rename_w.writerow([new_name, genomefile, ''])
                
                unclass_w.writerow([ident])
                n_unclass += 1

        print(f'n_unclass: {n_unclass}')
        print(f'n_classify: {n_classify}')
        print(f'found: {len(found)}')
        print(f'names: {len(NAMES)}')

        assert n_unclass + n_classify == len(NAMES), "are we missing something? (msg 1)?"
        assert len(found) == len(NAMES), "are we missing something (msg 2)?"


rule sketch:
    input:
        'manysketch-renamed.csv',
    output:
        'bin-sketches.renamed.sig.zip',
    threads: 64
    params:
        sketchtype="dna,k=21,k=31,k=51"
    shell: """
        sourmash scripts manysketch -c {threads} {input} -o {output} \
            -p {params.sketchtype}
    """

rule taxburst_genomes:
    input:
        sketches='bin-sketches.renamed.sig.zip',
        tax='bin-sketches.lineages.csv',
    output:
        'bin-sketches.taxburst.html',
    shell: """
        ./build-sourmash-db-view.py {input.sketches} -t {input.tax} \
           --max-rank-num=7 -o {output}
    """
